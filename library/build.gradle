buildscript {
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'


def supportVersion = rootProject.subprojects.ext.supportVersion[0]
def versionName = rootProject.subprojects.ext.versionName[0]
def javadocDir = new File(project.buildDir, "docs/javadocs/")
def releaseDir = new File(project.buildDir, "out/release/")


group = 'com.ruesga.timeline-chart-view'
version = versionName

android {
    dependencies {
        compile 'com.android.support:support-v4:' + supportVersion
    }

    defaultConfig {
        resourcePrefix 'tlc'
        consumerProguardFiles file('proguard.txt')
    }

    libraryVariants.all { variant ->
        def variantName = variant.baseName.capitalize()
        def outDir = new File(project.buildDir, "out/${variant.name}/")

        variant.outputs.each { output ->
            output.outputFile = new File(outDir, "timeline-chart-view-" + ((String) defaultConfig.versionName) + ".aar")
        }

        //noinspection GroovyAssignabilityCheck
        task "javadoc${variantName}" (type: Javadoc, dependsOn: "compile${variantName}Sources") {
            source = android.sourceSets.main.java.srcDirs
            def variantCompiledClassesDir = file("${buildDir}/intermediates/classes/${variant.dirName}/")
            classpath = files(
                    variant.javaCompile.classpath,
                    variantCompiledClassesDir,
                    android.bootClasspath
            )
            destinationDir = new File(javadocDir, (String) variant.name)

            options.author(true)
            options.version(true)
            options.use(true)
            options.setMemberLevel(JavadocMemberLevel.PUBLIC)
            options.links("http://docs.oracle.com/javase/7/docs/api/");
            options.links("http://developer.android.com/reference/");

            exclude '**/BuildConfig.java'
            exclude '**/R.java'
            exclude '**/helpers/*.java'
            failOnError false
        }
    }
}

//noinspection GroovyAssignabilityCheck
task "javadocJar"(type: Jar, dependsOn: "javadocRelease") {
    classifier = 'javadoc'
    from new File(javadocDir, "release")
    archiveName = "timeline-chart-view-" + versionName + "-javadoc.jar"
    destinationDir = releaseDir
}

//noinspection GroovyAssignabilityCheck
task "sourcesJar"(type: Jar, dependsOn: "compileReleaseSources") {
    classifier = 'sources'
    //noinspection GroovyAssignabilityCheck
    from android.sourceSets.main.java.srcDirs
    archiveName = "timeline-chart-view-" + versionName + "-sources.jar"
    destinationDir = releaseDir
}

task deploy (dependsOn: ["assembleRelease", "sourcesJar", "javadocJar", "install", "bintrayUpload"]) {
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'timeline-chart-view'
            description 'An android view to represent data over a timeline.'
            url 'https://github.com/jruesga/timeline-chart-view'
            inceptionYear '2015'

            packaging 'aar'
            groupId 'com.ruesga.timeline-chart-view'
            artifactId 'timeline-chart-view'
            version versionName

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/jruesga/timeline-chart-view.git'
                developerConnection 'https://github.com/jruesga/timeline-chart-view.git'
                url 'https://github.com/jruesga/timeline-chart-view'

            }
            developers {
                developer {
                    id 'jruesga'
                    name 'Jorge Ruesga'
                    email 'jorge@ruesga.com'
                }
            }
        }
    }
}

bintray {
    def Properties releaseProps = new Properties()
    releaseProps.load(new FileInputStream(rootProject.file('release.properties')))

    def Properties bintrayProps = new Properties()
    bintrayProps.load(new FileInputStream(new File((String) releaseProps['bintray.config'])))

    user = bintrayProps['bintray.user']
    key = bintrayProps['bintray.key']
    configurations = ['archives']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'timeline-chart-view'
        desc = 'An android view to represent data over a timeline.'
        websiteUrl = 'https://github.com/jruesga/timeline-chart-view'
        issueTrackerUrl = 'https://github.com/jruesga/timeline-chart-view/issues'
        vcsUrl = 'https://github.com/jruesga/timeline-chart-view.git'
        licenses = ['Apache-2.0']
        labels = ['android', 'charts', 'view', 'opensource', 'library']
        publicDownloadNumbers = true
        version {
            name = versionName
            desc = 'Release v' + versionName
            vcsTag = versionName
            gpg {
                sign = true
                passphrase = bintrayProps['bintray.gpg.passphrase']
            }
        }
    }
}
